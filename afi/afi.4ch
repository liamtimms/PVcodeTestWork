;===============================================================
;=
;= $Source: /pv/CvsTree/pv/gen/src/prg/methods/gre/afi.4ch,v $
;=
;= A gradient echo imaging pulse program
;=
;= $Id: afi.4ch,v 1.1.2.2 2007/04/25 06:12:25 mawi Exp $
;===============================================================


#include <MRI.include>
preset off


;===========================================================
;   definition of delays
;===========================================================

define delay denab
"denab =  d4 - de"

define delay d0m40u
"d0m40u = d0 - 40u"

"l3 = l0 + ds"

INIT_DEVICES


;===========================================================
;   declaration of 2d and 3d loop
;===========================================================


lgrad r2d<2d> = L[0]
zgrad r2d
lgrad r3d<3d> = L[1]
zgrad r3d


if(DS>0)
{
  dsl,  dgrad r2d
  lo to dsl times DS

}

lgrad slice = NSLICES
zslice


;===============================================================================
;	D/P               spec control          gradients	         
;===============================================================================
start, 	10u               fq8b:f1 fq8b:f2 fq8b:f3 fq8b:f4
 	d1		  fq1:f1 fq1:f2 fq1:f3  fq1:f4     grad{( 0)| (0) |(t0)}	 
       	d2 	          gatepulse 1
        (p0:sp0 ph1):f1
	d3                                      grad{(t2)| r2d(t3)|(t1)+r3d(t4)}
	d5		                        groff 	                 
	denab                   	        grad{(t5)| (0) | (0)} 
	                  ADC_INIT_B_4ch(ph1, ph0)  
	aqq               ADC_START_4ch
	d3                                      grad{(t8)| r2d(t6) | r3d(t7) } 		
	d6                                      grad{(t8)| (0) | (0) }
	d7		                        groff 
        d0m40u                
        ADC_END_4ch               
	1u                                      islice
lo to start times NSLICES
	1u		  ipp1                  zslice
lo to start times NA
	1u                rpp1                  igrad r2d
lo to start times l3
	1u                                      igrad r3d
   "l3=l0"
lo to start times l1
lo to start times NAE
SETUP_GOTO(start) 
exit
;============================================================
;phase lists

ph0 = 0
ph1 = 0 2 1 3
;============================================================
