;===============================================================
;=
;= $Source: /pv/CvsTree/pv/gen/src/prg/methods/demo/gre/afi_Play.ppg,v $
;=
;= A gradient echo imaging pulse program
;=
;= $Id: afi_Play.ppg,v 1.2 2013/09/06 12:38:39 sako Exp $
;===============================================================


#include <MRI.include>
preset off


;===========================================================
;   definition of delays
;===========================================================

define delay denab
"denab =  d4 - de"

"l3 = l0 + ds"

INIT_DEVICES


;===========================================================
;   declaration of 2d and 3d loop
;===========================================================

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>


if(DS>0)
{
  dsl,  r2d.dec
  lo to dsl times DS

}


;===============================================================================
;	D/P               spec control          gradients	         
;===============================================================================
start, 	10u               fq8(receive):f1
 	d1		  fq1:f1                grad_ramp{0, 0, g0}	 
       	d2 	          gatepulse 1
        (p0:sp0 ph1):f1
	d3                                      grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d5		                        grad_off
	denab                       	        grad_ramp{g5, 0, 0} 
	                  ADC_INIT_B(ph1, ph0)  
	aqq               ADC_START
	d3                                      grad_ramp{g8, r2d*g6, r3d*g7} 		
	d6                                      grad_ramp{g8, 0, 0}
	d7		                        grad_off
        d0                ADC_END               
	1u                                      grad_matrix.inc
lo to start times NSLICES
	1u		  ipp1                  grad_matrix.res
lo to start times NA
	1u                rpp1                  r2d.inc
lo to start times l3
	1u                                      r3d.inc
   "l3=l0"
lo to start times l1
lo to start times NAE
SETUP_GOTO(start) 
exit
;============================================================
;phase lists

ph0 = 0
ph1 = 0 2 1 3
;============================================================
